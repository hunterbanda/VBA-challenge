VERSION 1.0 CLASS
BEGIN
  MultiUse = -1  'True
END
Attribute VB_Name = "Sheet1"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = True
Sub Stock_analysis()
    Dim ws As Worksheet
    Dim total As Double
    Dim i As Long
    Dim change As Double
    Dim j As Integer
    Dim start As Long
    Dim rowCount As Long
    Dim percentChange As Double
    Dim increase_number As Long
    Dim decrease_number As Long
    Dim volume_number As Long
   
    ' Loop through each worksheet in the workbook
    For Each ws In ThisWorkbook.Sheets
        ' Set the current worksheet as active
        ws.Activate
       
        ' Set Title Row
        ws.Range("I1").Value = "Ticker"
        ws.Range("J1").Value = "Quarterly Change"
        ws.Range("K1").Value = "Percent Change"
        ws.Range("L1").Value = "Total Stock Volume"
        ws.Range("P1").Value = "Ticker"
        ws.Range("Q1").Value = "Value"
        ws.Range("O2").Value = "Greatest % Increase"
        ws.Range("O3").Value = "Greatest % Decrease"
        ws.Range("O4").Value = "Greatest Total Volume"
       
        ' Set Initial Values
        j = 0
        total = 0
        change = 0
        start = 2
       
        ' Get the row number of the last row with data
        rowCount = ws.Cells(ws.Rows.Count, "A").End(xlUp).Row
       
        For i = 2 To rowCount
            ' If ticker changes then print results
            If ws.Cells(i, 1).Value <> ws.Cells(i + 1, 1).Value Then
                total = total + ws.Cells(i, 7).Value
                If total = 0 Then
                    ws.Range("I" & 2 + j).Value = ws.Cells(i, 1).Value
                Else
                    ' Calculate Change
                    change = ws.Cells(i, 6).Value - ws.Cells(start, 3).Value
                    If ws.Cells(start, 3).Value <> 0 Then
                        percentChange = change / ws.Cells(start, 3).Value
                    Else
                        percentChange = 0
                    End If
                    start = i + 1
                    ws.Range("I" & 2 + j).Value = ws.Cells(i, 1).Value
                    ws.Range("J" & 2 + j).Value = change
                    ws.Range("K" & 2 + j).Value = percentChange
                    ws.Range("K" & 2 + j).NumberFormat = "0.00%"
                    ws.Range("L" & 2 + j).Value = total
                   
                    ' Color based on change
                    Select Case change
                        Case Is > 0
                            ws.Range("J" & 2 + j).Interior.ColorIndex = 4 ' Green
                        Case Is < 0
                            ws.Range("J" & 2 + j).Interior.ColorIndex = 3 ' Red
                        Case Else
                            ws.Range("J" & 2 + j).Interior.ColorIndex = 0 ' No color
                    End Select
                End If
               
                total = 0
                change = 0
                j = j + 1
            Else
                total = total + ws.Cells(i, 7).Value
            End If
        Next i
       
        ' Find the max, min, and highest volume
        On Error Resume Next
        ws.Range("Q2").Value = WorksheetFunction.Max(ws.Range("K2:K" & rowCount)) * 100
        ws.Range("Q3").Value = WorksheetFunction.Min(ws.Range("K2:K" & rowCount)) * 100
        ws.Range("Q4").Value = WorksheetFunction.Max(ws.Range("L2:L" & rowCount))
       
        increase_number = WorksheetFunction.Match(WorksheetFunction.Max(ws.Range("K2:K" & rowCount)), ws.Range("K2:K" & rowCount), 0)
        decrease_number = WorksheetFunction.Match(WorksheetFunction.Min(ws.Range("K2:K" & rowCount)), ws.Range("K2:K" & rowCount), 0)
        volume_number = WorksheetFunction.Match(WorksheetFunction.Max(ws.Range("L2:L" & rowCount)), ws.Range("L2:L" & rowCount), 0)
        On Error GoTo 0
       
        ' Output Ticker Symbols
        ws.Range("P2").Value = ws.Cells(increase_number + 1, 9).Value
        ws.Range("P3").Value = ws.Cells(decrease_number + 1, 9).Value
        ws.Range("P4").Value = ws.Cells(volume_number + 1, 9).Value
    Next ws
End Sub
